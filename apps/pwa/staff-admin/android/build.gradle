// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        kotlinVersion = '1.9.24'
    }
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.7.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.google.gms:google-services:4.4.1'
    }
}

apply from: "variables.gradle"

// Removed allprojects repositories - now handled by settings.gradle dependencyResolutionManagement

subprojects {
    configurations.all { config ->
        config.resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds'
            cacheDynamicVersionsFor 0, 'seconds'

            // Force consistent AndroidX versions for Capacitor 7
            force 'androidx.test:monitor:1.7.1'
            force 'androidx.concurrent:concurrent-futures:1.1.0'
            force 'androidx.tracing:tracing:1.1.0'
            force 'androidx.activity:activity:1.9.2'
            force 'org.jetbrains:annotations:23.0.0'
            force 'androidx.lifecycle:lifecycle-common:2.6.2'
            force 'androidx.arch.core:core-common:2.2.0'
            force 'androidx.arch.core:core-runtime:2.2.0'
            force 'androidx.startup:startup-runtime:1.1.1'
            force 'org.jetbrains.kotlinx:kotlinx-coroutines-core-jvm:1.7.3'
            force 'androidx.core:core:1.15.0'
            force 'androidx.core:core-splashscreen:1.0.1'
            force 'androidx.appcompat:appcompat:1.7.0'
            force 'androidx.webkit:webkit:1.12.1'
            force 'androidx.fragment:fragment:1.8.4'
            force 'androidx.coordinatorlayout:coordinatorlayout:1.2.0'
            force 'androidx.exifinterface:exifinterface:1.3.7'
            force 'com.google.android.material:material:1.12.0'
            force 'androidx.work:work-runtime-ktx:2.8.1'
            force 'androidx.biometric:biometric:1.1.0'
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
apply from: 'dependencies-constraints.gradle'

/**
 * Force Java 17 + Kotlin jvmTarget 17 for all Android modules.
 * This avoids "invalid source release: 21" when the toolchain is JDK 17.
 */
subprojects { project ->
  afterEvaluate {
    def hasAndroid = project.plugins.hasPlugin('com.android.application') || project.plugins.hasPlugin('com.android.library')
    if (hasAndroid) {
      project.android {
        compileOptions {
          sourceCompatibility JavaVersion.VERSION_17
          targetCompatibility JavaVersion.VERSION_17
        }
      }
      // Kotlin compile tasks
      project.tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
          jvmTarget = '17'
        }
      }
    }
  }
}
